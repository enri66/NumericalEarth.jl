using KernelAbstractions: @kernel, @index
using Oceananigans: initialize!
using Oceananigans.Architectures: architecture, on_architecture, CPU
using Oceananigans.BoundaryConditions: FieldBoundaryConditions
using Oceananigans.Units: Time
using Oceananigans.Grids: inactive_node, topology
using Oceananigans.OrthogonalSphericalShellGrids: OrthogonalSphericalShellGrids
using Oceananigans.Utils: launch!, KernelParameters
using Oceananigans.Operators: ℑxᶜᵃᵃ, ℑyᵃᶜᵃ
using Oceananigans.Units: Time

using ..EarthSystemModels: reference_density,
                           heat_capacity,
                           thermodynamics_parameters,
                           ocean_surface_temperature,
                           ocean_surface_salinity

#####
##### Container for organizing information related to fluxes
#####

mutable struct AtmosphereInterface{J, F, ST, SQ, P}
    fluxes :: J
    flux_formulation :: F
    temperature :: ST
    specific_humidity :: SQ
    properties :: P
end

"""
    SeaIceOceanInterface{J, F, T, S, P}

Container for sea ice-ocean interface data including fluxes, formulation, and interface state.

Fields
======

- `fluxes::J`: `SeaIceOceanFluxes` containing interface_heat, frazil_heat, salt, x_momentum, y_momentum
- `flux_formulation::F`: heat flux formulation (`IceBathHeatFlux` or `ThreeEquationHeatFlux`)
- `temperature::T`: interface temperature field (ocean surface view or computed field)
- `salinity::S`: interface salinity field (ocean surface view or computed field)
"""
mutable struct SeaIceOceanInterface{J, F, T, S}
    fluxes :: J
    flux_formulation :: F
    temperature :: T
    salinity :: S
end

# Utilities to get the computed fluxes
@inline computed_fluxes(interface::AtmosphereInterface)  = interface.fluxes
@inline computed_fluxes(interface::SeaIceOceanInterface) = interface.fluxes

vector_component_boundary_conditions(grid, loc) = FieldBoundaryConditions(grid, loc)

function vector_component_boundary_conditions(grid::OrthogonalSphericalShellGrids.TripolarGridOfSomeKind, loc)
    north_bc = OrthogonalSphericalShellGrids.north_fold_boundary_condition(grid, -1)
    return FieldBoundaryConditions(grid, loc; north = north_bc)
end

"""
    AtmosphereSurfaceFluxes{F}

Atmosphere↔surface turbulent flux container, shared by the atmosphere–ocean, atmosphere–land and
atmosphere–sea-ice interfaces: the five fluxes, and the three characteristic scales of the
Monin–Obukhov solve.
"""
struct AtmosphereSurfaceFluxes{F}
    latent_heat       :: F
    sensible_heat     :: F
    water_vapor       :: F
    x_momentum        :: F
    y_momentum        :: F
    friction_velocity :: F
    temperature_scale :: F
    water_vapor_scale :: F
end

function AtmosphereSurfaceFluxes(grid)
    F = Field{Center, Center, Nothing}
    velocity_bcs = vector_component_boundary_conditions(grid, (Center(), Center(), nothing))
    return AtmosphereSurfaceFluxes(F(grid), F(grid), F(grid),
                                   F(grid; boundary_conditions = velocity_bcs),
                                   F(grid; boundary_conditions = velocity_bcs),
                                   F(grid), F(grid), F(grid))
end

AtmosphereSurfaceFluxes(::Nothing) = AtmosphereSurfaceFluxes(ntuple(_ -> ZeroField(), 8)...)

Adapt.adapt_structure(to, fluxes::AtmosphereSurfaceFluxes) =
    AtmosphereSurfaceFluxes(Adapt.adapt(to, fluxes.latent_heat),
                            Adapt.adapt(to, fluxes.sensible_heat),
                            Adapt.adapt(to, fluxes.water_vapor),
                            Adapt.adapt(to, fluxes.x_momentum),
                            Adapt.adapt(to, fluxes.y_momentum),
                            Adapt.adapt(to, fluxes.friction_velocity),
                            Adapt.adapt(to, fluxes.temperature_scale),
                            Adapt.adapt(to, fluxes.water_vapor_scale))

Oceananigans.Architectures.on_architecture(arch, fluxes::AtmosphereSurfaceFluxes) =
    AtmosphereSurfaceFluxes(on_architecture(arch, fluxes.latent_heat),
                            on_architecture(arch, fluxes.sensible_heat),
                            on_architecture(arch, fluxes.water_vapor),
                            on_architecture(arch, fluxes.x_momentum),
                            on_architecture(arch, fluxes.y_momentum),
                            on_architecture(arch, fluxes.friction_velocity),
                            on_architecture(arch, fluxes.temperature_scale),
                            on_architecture(arch, fluxes.water_vapor_scale))

struct SeaIceOceanFluxes{C, FX, FY}
    interface_heat :: C
    frazil_heat    :: C
    salt           :: C
    freshwater     :: C
    freshwater_heat_content :: C
    # Affine ice-ocean drag Fₑ + λ uᵒ: `x_momentum` is Fₑ = -ρₑ Cᴰ |Δu| uⁱ, `x_momentum_coefficient` is λ = ρₑ Cᴰ |Δu|.
    x_momentum             :: FX
    y_momentum             :: FY
    x_momentum_coefficient :: FX
    y_momentum_coefficient :: FY
end

function SeaIceOceanFluxes(grid)
    C  = Field{Center, Center, Nothing}
    x_velocity_bcs = vector_component_boundary_conditions(grid, (Face(), Center(), nothing))
    y_velocity_bcs = vector_component_boundary_conditions(grid, (Center(), Face(), nothing))
    return SeaIceOceanFluxes(C(grid), C(grid), C(grid), C(grid), C(grid),
                             Field{Face, Center, Nothing}(grid; boundary_conditions = x_velocity_bcs),
                             Field{Center, Face, Nothing}(grid; boundary_conditions = y_velocity_bcs),
                             Field{Face, Center, Nothing}(grid),
                             Field{Center, Face, Nothing}(grid))
end

SeaIceOceanFluxes(::Nothing) = SeaIceOceanFluxes(ntuple(_ -> ZeroField(), 9)...)

Adapt.adapt_structure(to, fluxes::SeaIceOceanFluxes) =
    SeaIceOceanFluxes(Adapt.adapt(to, fluxes.interface_heat),
                      Adapt.adapt(to, fluxes.frazil_heat),
                      Adapt.adapt(to, fluxes.salt),
                      Adapt.adapt(to, fluxes.freshwater),
                      Adapt.adapt(to, fluxes.freshwater_heat_content),
                      Adapt.adapt(to, fluxes.x_momentum),
                      Adapt.adapt(to, fluxes.y_momentum),
                      Adapt.adapt(to, fluxes.x_momentum_coefficient),
                      Adapt.adapt(to, fluxes.y_momentum_coefficient))

Oceananigans.Architectures.on_architecture(arch, fluxes::SeaIceOceanFluxes) =
    SeaIceOceanFluxes(on_architecture(arch, fluxes.interface_heat),
                      on_architecture(arch, fluxes.frazil_heat),
                      on_architecture(arch, fluxes.salt),
                      on_architecture(arch, fluxes.freshwater),
                      on_architecture(arch, fluxes.freshwater_heat_content),
                      on_architecture(arch, fluxes.x_momentum),
                      on_architecture(arch, fluxes.y_momentum),
                      on_architecture(arch, fluxes.x_momentum_coefficient),
                      on_architecture(arch, fluxes.y_momentum_coefficient))

# ZeroFluxes is returned by computed_fluxes(::Nothing) for absent interfaces.
# It contains the union of all flux field names across interface types.
struct ZeroFluxes{Z}
    # Atmosphere-ocean and atmosphere-sea-ice flux fields (turbulent only;
    # radiative diagnostic fields live on the radiation component)
    latent_heat             :: Z
    sensible_heat           :: Z
    water_vapor             :: Z
    x_momentum              :: Z
    y_momentum              :: Z
    friction_velocity       :: Z
    temperature_scale       :: Z
    water_vapor_scale       :: Z
    # Sea ice-ocean flux fields
    interface_heat          :: Z
    frazil_heat             :: Z
    salt                    :: Z
    freshwater              :: Z
    freshwater_heat_content :: Z
    x_momentum_coefficient  :: Z
    y_momentum_coefficient  :: Z
end

ZeroFluxes() = ZeroFluxes(ntuple(_ -> ZeroField(), 15)...)

@inline computed_fluxes(::Nothing) = ZeroFluxes()

mutable struct ComponentInterfaces{AO, ASI, SIO, AL, C, AP, OP, SIP, EX, P}
    atmosphere_ocean_interface :: AO
    atmosphere_sea_ice_interface :: ASI
    sea_ice_ocean_interface :: SIO
    atmosphere_land_interface :: AL
    atmosphere_properties :: AP
    ocean_properties :: OP
    sea_ice_properties :: SIP
    exchanger :: EX
    net_fluxes :: C
    properties :: P
end

using ..EarthSystemModels: DegreesCelsius, temperature_units, exchange_grid,
                           celsius_to_kelvin, convert_to_kelvin, convert_from_kelvin

Base.summary(crf::ComponentInterfaces) = "ComponentInterfaces"
Base.show(io::IO, crf::ComponentInterfaces) = print(io, summary(crf))

# Diagnostic surface (skin) temperature — what the atmosphere "sees"; for skin-temperature
# closures it differs from `land.temperature`. The land interface wins; then the
# atmosphere-ocean interface; `nothing` when the atmosphere touches neither.
EarthSystemModels.surface_temperature(interface::AtmosphereInterface) = interface.temperature
EarthSystemModels.surface_temperature(interfaces::ComponentInterfaces) =
    EarthSystemModels.surface_temperature(interfaces.atmosphere_land_interface,
                                          interfaces.atmosphere_ocean_interface)

EarthSystemModels.surface_temperature(land_interface::AtmosphereInterface, ocean_interface) =
    land_interface.temperature
EarthSystemModels.surface_temperature(::Nothing, ocean_interface::AtmosphereInterface) =
    ocean_interface.temperature
EarthSystemModels.surface_temperature(::Nothing, ::Nothing) = nothing

#####
##### Atmosphere-Ocean Interface
#####

atmosphere_ocean_interface(grid, ::Nothing,   ocean,    args...) = nothing
atmosphere_ocean_interface(grid, ::Nothing,  ::Nothing, args...) = nothing
atmosphere_ocean_interface(grid, atmosphere, ::Nothing, args...) = nothing

function atmosphere_ocean_interface(grid,
                                    atmosphere,
                                    ocean,
                                    ao_flux_formulation,
                                    temperature_formulation,
                                    velocity_formulation,
                                    specific_humidity_formulation)

    ao_fluxes = AtmosphereSurfaceFluxes(grid)

    ao_properties = InterfaceProperties(specific_humidity_formulation,
                                        temperature_formulation,
                                        velocity_formulation)

    interface_temperature = Field{Center, Center, Nothing}(grid)
    interface_specific_humidity = Field{Center, Center, Nothing}(grid)

    return AtmosphereInterface(ao_fluxes, ao_flux_formulation, interface_temperature,
                               interface_specific_humidity, ao_properties)
end

#####
##### Atmosphere-Sea Ice Interface
#####

atmosphere_sea_ice_interface(grid, atmos, ::Nothing,     args...) = nothing
atmosphere_sea_ice_interface(grid, ::Nothing, sea_ice,   args...) = nothing
atmosphere_sea_ice_interface(grid, ::Nothing, ::Nothing, args...) = nothing

function atmosphere_sea_ice_interface(grid,
                                      atmosphere,
                                      sea_ice,
                                      ai_flux_formulation,
                                      temperature_formulation,
                                      velocity_formulation)

    fluxes = AtmosphereSurfaceFluxes(grid)

    phase = AtmosphericThermodynamics.Ice()
    specific_humidity_formulation = ImpureSaturationSpecificHumidity(phase)

    properties = InterfaceProperties(specific_humidity_formulation,
                                     temperature_formulation,
                                     velocity_formulation)

    snow_thermo = sea_ice.model.snow_thermodynamics
    interface_temperature = if isnothing(snow_thermo)
        sea_ice.model.ice_thermodynamics.top_surface_temperature
    else
        snow_thermo.top_surface_temperature
    end

    interface_specific_humidity = Field{Center, Center, Nothing}(grid)

    return AtmosphereInterface(fluxes, ai_flux_formulation, interface_temperature,
                               interface_specific_humidity, properties)
end

#####
##### Sea Ice-Ocean Interface
#####

sea_ice_ocean_interface(grid, ::Nothing, ocean,     flux_formulation; kwargs...) = nothing
sea_ice_ocean_interface(grid, ::Nothing, ::Nothing, flux_formulation; kwargs...) = nothing
sea_ice_ocean_interface(grid, sea_ice,   ::Nothing, flux_formulation; kwargs...) = nothing

# Disambiguation
sea_ice_ocean_interface(grid, ::Nothing,     ocean, ::ThreeEquationHeatFlux; kwargs...) = nothing
sea_ice_ocean_interface(grid, sea_ice,   ::Nothing, ::ThreeEquationHeatFlux; kwargs...) = nothing
sea_ice_ocean_interface(grid, ::Nothing, ::Nothing, ::ThreeEquationHeatFlux; kwargs...) = nothing

"""
    sea_ice_ocean_interface(grid, sea_ice, ocean, flux_formulation)

Construct a `SeaIceOceanInterface` with the specified flux formulation.

For `IceBathHeatFlux`, the interface temperature and salinity
point to the ocean surface values. For `ThreeEquationHeatFlux`, dedicated fields are
created to store the computed interface values.

Arguments
=========

- `grid`: the computational grid
- `sea_ice`: sea ice simulation
- `ocean`: ocean simulation
- `flux_formulation`: heat flux formulation (`IceBathHeatFlux` or `ThreeEquationHeatFlux`)
"""
function sea_ice_ocean_interface(grid, sea_ice, ocean, flux_formulation)
    io_fluxes = SeaIceOceanFluxes(grid)

    # For default flux formulations, interface temperature and salinity point to ocean surface
    Tⁱⁿ = ocean_surface_temperature(ocean)
    Sⁱⁿ = ocean_surface_salinity(ocean)

    return SeaIceOceanInterface(io_fluxes, flux_formulation, Tⁱⁿ, Sⁱⁿ)
end

function sea_ice_ocean_interface(grid, sea_ice, ocean, flux_formulation::ThreeEquationHeatFlux)
    io_fluxes = SeaIceOceanFluxes(grid)

    # Interface temperature and salinity are computed fields
    Tⁱⁿ = Field{Center, Center, Nothing}(grid)
    Sⁱⁿ = Field{Center, Center, Nothing}(grid)

    return SeaIceOceanInterface(io_fluxes, flux_formulation, Tⁱⁿ, Sⁱⁿ)
end

#####
##### Component Interfaces
#####

default_ai_temperature(::Nothing) = nothing
biogeochemical_interface(exchanger, ocean; kwargs...) = biogeochemical_interface(exchanger, ocean, ocean.model.biogeochemistry; kwargs...)
biogeochemical_interface(exchanger, ocean::Nothing; kwargs...) = NamedTuple()
biogeochemical_interface(exchanger, ocean, biogeochemistry; kwargs...) = NamedTuple()

function default_ao_specific_humidity(ocean)
    FT    = eltype(ocean)
    phase = AtmosphericThermodynamics.Liquid()
    x_H₂O = convert(FT, 0.98)
    return ImpureSaturationSpecificHumidity(phase, x_H₂O)
end

"""
    ComponentInterfaces(atmosphere, ocean, sea_ice=nothing; kwargs...)

Construct component interfaces for atmosphere-ocean-sea ice coupling.

Keyword Arguments
=================

- `sea_ice_ocean_heat_flux`: formulation for sea ice-ocean heat flux. Options are:
  - `IceBathHeatFlux()`: bulk heat flux with interface at freezing point
  - `ThreeEquationHeatFlux()`: coupled heat/salt/freezing point system (default)

- `radiation`: radiation component. Default: `nothing`.
- `freshwater_density`: reference density of freshwater. Default: `default_freshwater_density`.
- `latent_heat_of_fusion`: latent heat [J kg⁻¹] the ocean supplies to melt snowfall and icebergs. Default: `default_latent_heat_of_fusion`.
- `atmosphere_ocean_fluxes`: flux formulation for atmosphere-ocean interface. Default: `SimilarityTheoryFluxes()`.
- `atmosphere_sea_ice_fluxes`: flux formulation for atmosphere-sea ice interface. Default: `SimilarityTheoryFluxes()`.
- `atmosphere_ocean_interface_temperature`: temperature formulation for atmosphere-ocean interface.
   Options are `BulkTemperature()` (default) and `SkinTemperature(internal_flux)`, where for the ocean
   `internal_flux` is a `DiffusiveFlux(κ, δ)` with either a prescribed diffusivity `κ` or an
   `InteriorDiffusivity()` assessed from the ocean turbulence closure.
- `atmosphere_ocean_interface_specific_humidity`: specific humidity formulation. Default: `default_ao_specific_humidity(ocean)`.
- `atmosphere_sea_ice_interface_temperature`: temperature formulation for atmosphere-sea ice interface. Default: `default_ai_temperature(sea_ice)`.
- `ocean_reference_density`: reference density for the ocean. Default: `reference_density(ocean)`.
- `ocean_heat_capacity`: heat capacity for the ocean. Default: `heat_capacity(ocean)`.
- `ocean_temperature_units`: temperature units for the ocean. Default: `DegreesCelsius()`.
- `sea_ice_temperature_units`: temperature units for sea ice. Default: `DegreesCelsius()`.
- `sea_ice_reference_density`: reference density for sea ice. Default: `reference_density(sea_ice)`.
- `sea_ice_heat_capacity`: heat capacity for sea ice. Default: `heat_capacity(sea_ice)`.
- `gravitational_acceleration`: gravitational acceleration. Default: `default_gravitational_acceleration`.
"""
function ComponentInterfaces(atmosphere, ocean, sea_ice=nothing;
                             radiation = nothing,
                             land = nothing,
                             exchange_grid = exchange_grid(atmosphere, ocean, sea_ice, land),
                             freshwater_density = default_freshwater_density,
                             latent_heat_of_fusion = default_latent_heat_of_fusion,
                             atmosphere_ocean_fluxes = SimilarityTheoryFluxes(eltype(exchange_grid)),
                             atmosphere_sea_ice_fluxes = atmosphere_sea_ice_similarity_theory(eltype(exchange_grid)),
                             atmosphere_land_fluxes = default_atmosphere_land_fluxes(land, eltype(exchange_grid)),
                             sea_ice_ocean_heat_flux = ThreeEquationHeatFlux(sea_ice),
                             atmosphere_ocean_interface_temperature = BulkTemperature(),
                             atmosphere_ocean_velocity_difference = RelativeVelocity(),
                             atmosphere_ocean_interface_specific_humidity = default_ao_specific_humidity(ocean),
                             atmosphere_sea_ice_interface_temperature = default_ai_temperature(sea_ice),
                             atmosphere_sea_ice_velocity_difference = RelativeVelocity(),
                             atmosphere_land_interface_temperature = BulkTemperature(),
                             atmosphere_land_velocity_difference = RelativeVelocity(),
                             atmosphere_land_interface_specific_humidity = default_al_specific_humidity(land),
                             atmosphere_land_interface = atmosphere_land_interface(exchange_grid, atmosphere, land;
                                                                                   fluxes              = atmosphere_land_fluxes,
                                                                                   temperature         = atmosphere_land_interface_temperature,
                                                                                   velocity_difference = atmosphere_land_velocity_difference,
                                                                                   specific_humidity   = atmosphere_land_interface_specific_humidity),
                             ocean_reference_density = reference_density(ocean),
                             ocean_heat_capacity = heat_capacity(ocean),
                             ocean_temperature_units = temperature_units(ocean),
                             sea_ice_temperature_units = DegreesCelsius(),
                             sea_ice_reference_density = reference_density(sea_ice),
                             sea_ice_heat_capacity = heat_capacity(sea_ice),
                             gravitational_acceleration = default_gravitational_acceleration,
                             exchanger_correction = nothing,
                             biogeochemistry_interface_kwargs = NamedTuple())

    FT = eltype(exchange_grid)

    ocean_reference_density    = convert(FT, ocean_reference_density)
    ocean_heat_capacity        = convert(FT, ocean_heat_capacity)
    sea_ice_reference_density  = convert(FT, sea_ice_reference_density)
    sea_ice_heat_capacity      = convert(FT, sea_ice_heat_capacity)
    freshwater_density         = convert(FT, freshwater_density)
    latent_heat_of_fusion      = convert(FT, latent_heat_of_fusion)
    gravitational_acceleration = convert(FT, gravitational_acceleration)

    # Component properties
    atmosphere_properties = thermodynamics_parameters(atmosphere)

    ocean_properties = (reference_density     = ocean_reference_density,
                        heat_capacity         = ocean_heat_capacity,
                        freshwater_density    = freshwater_density,
                        latent_heat_of_fusion = latent_heat_of_fusion,
                        temperature_units     = ocean_temperature_units)

    # Only build sea_ice_properties if sea_ice is an actual Simulation with a model
    if sea_ice isa Simulation
        sea_ice_properties = (reference_density  = sea_ice_reference_density,
                              heat_capacity      = sea_ice_heat_capacity,
                              freshwater_density = freshwater_density,
                              liquidus           = sea_ice.model.phase_transitions.liquidus,
                              temperature_units  = sea_ice_temperature_units)
    else
        sea_ice_properties = nothing
    end

    # Component interfaces
    ao_interface = atmosphere_ocean_interface(exchange_grid,
                                              atmosphere,
                                              ocean,
                                              atmosphere_ocean_fluxes,
                                              atmosphere_ocean_interface_temperature,
                                              atmosphere_ocean_velocity_difference,
                                              atmosphere_ocean_interface_specific_humidity)

    io_interface = sea_ice_ocean_interface(exchange_grid, sea_ice, ocean, sea_ice_ocean_heat_flux)

    ai_interface = atmosphere_sea_ice_interface(exchange_grid,
                                                atmosphere,
                                                sea_ice,
                                                atmosphere_sea_ice_fluxes,
                                                atmosphere_sea_ice_interface_temperature,
                                                atmosphere_sea_ice_velocity_difference)

    # `atmosphere_land_interface` is either user-supplied or built from the four
    # sibling kwargs above by the same-named keyword default.
    al_interface = atmosphere_land_interface
    # Total interface fluxes
    total_fluxes = (ocean      = net_fluxes(ocean),
                    sea_ice    = net_fluxes(sea_ice),
                    atmosphere = net_fluxes(atmosphere))

    exchanger = StateExchanger(exchange_grid, radiation, atmosphere, land, ocean, sea_ice;
                               atmosphere_correction = exchanger_correction)

    # The surface-layer (MOST reference) height is fixed by the atmosphere grid, so
    # build it once here rather than per coupled step. Scalar for prescribed
    # atmospheres; a per-column 2-D field for grid-aware atmospheres (Breeze). The
    # boundary-layer height is *not* cached here: it evolves with the closure and is
    # refreshed every step in the flux builders.
    zᵃᵗ = surface_layer_height(atmosphere, exchange_grid)

    for interface in (ao_interface, ai_interface, al_interface)
        isnothing(interface) || validate_zero_plane_displacement(interface.flux_formulation, zᵃᵗ)
    end

    properties = merge((; gravitational_acceleration, surface_layer_height = zᵃᵗ),
                          biogeochemical_interface(exchanger, ocean; biogeochemistry_interface_kwargs...))

    return ComponentInterfaces(ao_interface,
                               ai_interface,
                               io_interface,
                               al_interface,
                               atmosphere_properties,
                               ocean_properties,
                               sea_ice_properties,
                               exchanger,
                               total_fluxes,
                               properties)
end

# Default land surface humidity formulation: bulk (saturated where wet, dry
# otherwise). The binary saturation is read from `saturation` per cell
# by the flux kernel and threaded through the iteration's `S` slot.
default_al_specific_humidity(::Nothing) = nothing
default_al_specific_humidity(land) =
    BulkHumidity(AtmosphericThermodynamics.Liquid())

# Default atmosphere--land flux formulation. Aerodynamic roughness lengths and the
# zero-plane displacement are properties of the flux closure, not the land model:
# the defaults below are uniform constants (0.1 m momentum, 0.01 m scalar, no
# displacement). Override per-domain by passing `atmosphere_land_fluxes =
# SimilarityTheoryFluxes(...)` with explicit roughness lengths and displacement
# (constants, or `Field`s of per-cell values) to `ComponentInterfaces` /
# `AtmosphereLandModel`.
default_atmosphere_land_fluxes(::Nothing, FT; kw...) = nothing

function default_atmosphere_land_fluxes(land, FT; solver_stop_criteria = nothing)
    return SimilarityTheoryFluxes(FT;
                                   stability_functions          = atmosphere_land_stability_functions(FT),
                                   momentum_roughness_length    = convert(FT, 0.1),
                                   temperature_roughness_length = convert(FT, 0.01),
                                   water_vapor_roughness_length = convert(FT, 0.01),
                                   solver_stop_criteria)
end

#####
##### Chekpointing (not needed for ComponentInterfaces)
#####

Oceananigans.prognostic_state(::ComponentInterfaces) = nothing
Oceananigans.restore_prognostic_state!(ci::ComponentInterfaces, state) = ci
Oceananigans.restore_prognostic_state!(ci::ComponentInterfaces, ::Nothing) = ci

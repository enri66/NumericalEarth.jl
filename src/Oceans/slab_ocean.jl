using Oceananigans.BoundaryConditions: fill_halo_regions!
using Oceananigans.Fields: ConstantField, ZeroField

struct SlabOcean{FT, G, Clk, T, F, H, ρ, C}
    grid :: G
    clock :: Clk
    temperature :: T
    temperature_flux :: F
    depth :: H
    density :: ρ
    heat_capacity :: C
end

"""
    SlabOcean(grid;
              FT = eltype(grid),
              depth = 50,
              density = 1025,
              heat_capacity = 4000,
              clock = Clock{FT}(time=0))

A slab ocean component for `EarthSystemModel`. Represents the ocean as a single
well-mixed layer of fixed depth, with temperature evolving in response to
net surface heat fluxes:

    ∂T/∂t = Q / (ρ cₚ H)

where `Q` is the net downward surface heat flux (W/m²), `ρ` is the seawater density,
`cₚ` is the heat capacity, and `H` is the slab depth.

Internally, the `EarthSystemModel` coupling assembles a temperature flux `Jᵀ` (in K m/s),
so the tendency is computed as `∂T/∂t = -Jᵀ / H`.

Use `set!(ocean, T=value)` to initialize the slab temperature after construction.

Arguments
=========

- `grid`: An Oceananigans grid for the slab ocean domain.

Keyword Arguments
=================

- `FT`: Floating point type for the slab ocean fields. Default: `eltype(grid)`.
- `depth`: Depth of the slab in meters. Default: 50.
- `density`: Seawater density in kg/m³. Default: 1025.
- `heat_capacity`: Seawater specific heat capacity in J/(kg·K). Default: 4000.
- `clock`: Clock for tracking slab ocean time. Default: `Clock{FT}(time=0)` where `FT` is the
  float type of the grid.
"""
function SlabOcean(grid;
                   FT = eltype(grid),
                   depth = 50,
                   density = 1025,
                   heat_capacity = 4000,
                   clock = Clock{FT}(time=0))

    temperature = CenterField(grid)
    temperature_flux = CenterField(grid)

    return SlabOcean{FT}(grid, clock, temperature,
                         temperature_flux, depth, density, heat_capacity)
end

# Inner constructor that captures FT
SlabOcean{FT}(grid, clock, T, tf, d, ρ, c) where FT =
    SlabOcean{FT, typeof(grid), typeof(clock), typeof(T),
              typeof(tf), typeof(d), typeof(ρ), typeof(c)}(grid, clock, T, tf, d, ρ, c)

function Oceananigans.set!(ocean::SlabOcean; T=nothing)
    !isnothing(T) && set!(ocean.temperature, T)
    return nothing
end

function Base.summary(ocean::SlabOcean{FT}) where FT
    A = nameof(typeof(architecture(ocean.grid)))
    G = nameof(typeof(ocean.grid))
    return string("SlabOcean{$FT, $A, $G}",
                  "(time = ", prettytime(ocean.clock.time), ", iteration = ", ocean.clock.iteration, ")")
end

function Base.show(io::IO, ocean::SlabOcean)
    print(io, summary(ocean), "\n",
          "├── grid: ", summary(ocean.grid), "\n",
          "├── depth: ", prettysummary(ocean.depth), "\n",
          "├── density: ", prettysummary(ocean.density), "\n",
          "└── heat_capacity: ", prettysummary(ocean.heat_capacity))
end
Base.eltype(::SlabOcean{FT}) where FT = FT

Grids.grid(ocean::SlabOcean) = ocean.grid

#####
##### EarthSystemModels interface
#####

EarthSystemModels.reference_density(ocean::SlabOcean) = ocean.density
EarthSystemModels.heat_capacity(ocean::SlabOcean) = ocean.heat_capacity
EarthSystemModels.exchange_grid(atmosphere, ocean::SlabOcean, sea_ice, land=nothing) = ocean.grid
EarthSystemModels.temperature_units(::SlabOcean) = DegreesKelvin()
EarthSystemModels.ocean_temperature(ocean::SlabOcean) = ocean.temperature
EarthSystemModels.ocean_salinity(ocean::SlabOcean{FT}) where FT = ConstantField(convert(FT, 35))
EarthSystemModels.ocean_surface_temperature(ocean::SlabOcean) = ocean.temperature
EarthSystemModels.ocean_surface_salinity(ocean::SlabOcean{FT}) where FT = ConstantField(convert(FT, 35))
EarthSystemModels.ocean_surface_velocities(::SlabOcean{FT}) where FT = ZeroField(FT), ZeroField(FT)

#####
##### InterfaceComputations interface
#####

function EarthSystemModels.InterfaceComputations.ComponentExchanger(ocean::SlabOcean, exchange_grid)
    T = ocean.temperature
    S = ocean_surface_salinity(ocean)
    u, v = ocean_surface_velocities(ocean)
    return ComponentExchanger((; u, v, T, S), nothing)
end

function EarthSystemModels.InterfaceComputations.net_fluxes(ocean::SlabOcean)
    grid = ocean.grid
    Jˢ = CenterField(grid)
    τx = CenterField(grid)
    τy = CenterField(grid)
    Jʷ = CenterField(grid)
    Jᴴ = CenterField(grid)
    return (T=ocean.temperature_flux, S=Jˢ, u=τx, v=τy, η=Jʷ, freshwater_heat_content=Jᴴ, u_coefficient=nothing, v_coefficient=nothing)
end

# No interpolation needed: the slab ocean IS on the exchange grid
EarthSystemModels.interpolate_state!(exchanger, grid, ::SlabOcean, coupled_model) = nothing
EarthSystemModels.InterfaceComputations.biogeochemical_interface(exchanger, ocean::SlabOcean; kwargs...) = NamedTuple()

# Assemble net ocean fluxes from interface computations
EarthSystemModels.update_net_fluxes!(coupled_model, ocean::SlabOcean) =
    update_net_ocean_fluxes!(coupled_model, ocean, ocean.grid)

# `FT` is not inferable from the fields, so rebuild explicitly to preserve it.
function EarthSystemModels.adopt_clock(ocean::SlabOcean{FT}, clock) where FT
    new_clock = EarthSystemModels.matching_clock(ocean.clock, clock)
    isnothing(new_clock) && return ocean
    EarthSystemModels.warn_clock_coercion(ocean, new_clock)
    return SlabOcean{FT}(ocean.grid, new_clock, ocean.temperature,
                         ocean.temperature_flux, ocean.depth, ocean.density, ocean.heat_capacity)
end

#####
##### Time stepping
#####

@kernel function _step_slab_temperature!(T, Jᵀ, Δt, H)
    i, j = @index(Global, NTuple)
    @inbounds T[i, j, 1] -= Jᵀ[i, j, 1] * Δt / H
end

function Oceananigans.TimeSteppers.time_step!(ocean::SlabOcean, Δt)
    tick!(ocean.clock, Δt)
    grid = ocean.grid
    arch = architecture(grid)
    launch!(arch, grid, :xy, _step_slab_temperature!,
            ocean.temperature, ocean.temperature_flux, Δt, ocean.depth)
    fill_halo_regions!(ocean.temperature)
    return nothing
end

#####
##### Checkpointing
#####

Oceananigans.prognostic_state(ocean::SlabOcean) = (; T = Array(interior(ocean.temperature)))

function Oceananigans.restore_prognostic_state!(ocean::SlabOcean, state)
    interior(ocean.temperature) .= state.T
    return ocean
end

Oceananigans.restore_prognostic_state!(ocean::SlabOcean, ::Nothing) = ocean

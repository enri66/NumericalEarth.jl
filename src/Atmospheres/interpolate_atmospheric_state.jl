using Oceananigans.Operators: intrinsic_vector
using Oceananigans.Fields: FractionalIndices, interpolate
using Oceananigans.OutputReaders: cpu_interpolating_time_indices

using ..Oceans: forcing_barotropic_potential

"""Interpolate the atmospheric state onto the ocean / sea-ice grid."""
function EarthSystemModels.interpolate_state!(exchanger, grid, atmosphere::PrescribedAtmosphere, coupled_model)
    atmosphere_grid = atmosphere.grid

    # Basic model properties
    arch = architecture(grid)
    clock = coupled_model.clock

    #####
    ##### First interpolate atmosphere time series
    ##### in time and to the ocean grid.
    #####

    # We use .data here to save parameter space (unlike Field, adapt_structure for
    # fts = FieldTimeSeries does not return fts.data)
    atmosphere_time_series = (u  = field_data(atmosphere.velocities.u),
                              v  = field_data(atmosphere.velocities.v),
                              T  = field_data(atmosphere.temperature),
                              q  = field_data(atmosphere.specific_humidity),
                              p  = field_data(atmosphere.pressure),
                              Jʳ = surface_rainfall_flux(atmosphere),
                              Jˢ = surface_snowfall_flux(atmosphere))

    atmosphere_fields = exchanger.state
    space_fractional_indices = exchanger.regridder

    # Simplify NamedTuple to reduce parameter space consumption.
    # See https://github.com/CliMA/NumericalEarth.jl/issues/116.
    atmosphere_data = (u   = atmosphere_fields.u.data,
                       v   = atmosphere_fields.v.data,
                       T   = atmosphere_fields.T.data,
                       p   = atmosphere_fields.p.data,
                       q   = atmosphere_fields.q.data,
                       Jʳⁿ = atmosphere_fields.Jʳⁿ.data,
                       Jˢⁿ = atmosphere_fields.Jˢⁿ.data)

    kernel_parameters = interface_kernel_parameters(grid)

    # Assumption, should be generalized
    ua = atmosphere.velocities.u

    times = ua.times
    time_indexing = ua.time_indexing
    t = clock.time
    time_interpolator = cpu_interpolating_time_indices(arch, times, time_indexing, t)

    launch!(arch, grid, kernel_parameters,
            _interpolate_primary_atmospheric_state!,
            atmosphere_data,
            space_fractional_indices,
            time_interpolator,
            grid,
            atmosphere_time_series)

    # Set ocean barotropic pressure forcing
    #
    # TODO: find a better design for this that doesn't have redundant
    # arrays for the barotropic potential
    potential = forcing_barotropic_potential(coupled_model.ocean)
    ρᵒᶜ = coupled_model.interfaces.ocean_properties.reference_density

    if !isnothing(potential)
        parent(potential) .= parent(atmosphere_data.p) ./ ρᵒᶜ
    end
end

@inline get_fractional_index(i, j, ::Nothing) = nothing
@inline get_fractional_index(i, j, frac) = @inbounds frac[i, j, 1]

@kernel function _interpolate_primary_atmospheric_state!(surface_atmos_state,
                                                         space_fractional_indices,
                                                         time_interpolator,
                                                         exchange_grid,
                                                         atmos_time_series)

    i, j = @index(Global, NTuple)

    ii = space_fractional_indices.i
    jj = space_fractional_indices.j
    fi = get_fractional_index(i, j, ii)
    fj = get_fractional_index(i, j, jj)

    x_itp = FractionalIndices(fi, fj, nothing)
    t_itp = time_interpolator

    uᵃᵗ = interp_atmos_time_series(atmos_time_series.u, x_itp, t_itp)
    vᵃᵗ = interp_atmos_time_series(atmos_time_series.v, x_itp, t_itp)
    Tᵃᵗ = interp_atmos_time_series(atmos_time_series.T, x_itp, t_itp)
    qᵃᵗ = interp_atmos_time_series(atmos_time_series.q, x_itp, t_itp)
    pᵃᵗ = interp_atmos_time_series(atmos_time_series.p, x_itp, t_itp)

    Mr = interp_atmos_time_series(atmos_time_series.Jʳ, x_itp, t_itp)
    Ms = interp_atmos_time_series(atmos_time_series.Jˢ, x_itp, t_itp)

    # Convert atmosphere velocities (usually defined on a latitude-longitude grid) to
    # the frame of reference of the native grid
    kᴺ = size(exchange_grid, 3) # index of the top ocean cell
    uᵃᵗ, vᵃᵗ = intrinsic_vector(i, j, kᴺ, exchange_grid, uᵃᵗ, vᵃᵗ)

    @inbounds begin
        surface_atmos_state.u[i, j, 1] = uᵃᵗ
        surface_atmos_state.v[i, j, 1] = vᵃᵗ
        surface_atmos_state.T[i, j, 1] = Tᵃᵗ
        surface_atmos_state.p[i, j, 1] = pᵃᵗ
        surface_atmos_state.q[i, j, 1] = qᵃᵗ
        surface_atmos_state.Jʳⁿ[i, j, 1] = Mr
        surface_atmos_state.Jˢⁿ[i, j, 1] = Ms
    end
end

#####
##### Utility for interpolating tuples of fields
#####

@inline interp_atmos_time_series(::Nothing, args...) = 0

@inline interp_atmos_time_series(J::NamedTuple{(:data, :backend, :time_indexing)}, X, time) =
    interpolate(X, time, J.data, J.backend, J.time_indexing)

# Note: assumes loc = (c, c, nothing) (and the third location should not matter.)
@inline interp_atmos_time_series(J::AbstractArray, X::FractionalIndices, time, args...) =
    interpolate(X, time, J, args...)

@inline interp_atmos_time_series(J::AbstractArray, X, time, grid, args...) =
    interpolate(X, time, J, (Center(), Center(), nothing), grid, args...)

@inline interp_atmos_time_series(ΣJ::NamedTuple, args...) =
    interp_atmos_time_series(values(ΣJ), args...)

@inline interp_atmos_time_series(ΣJ::Tuple{<:Any}, args...) =
    interp_atmos_time_series(ΣJ[1], args...)

@inline interp_atmos_time_series(ΣJ::Tuple{<:Any, <:Any}, args...) =
    interp_atmos_time_series(ΣJ[1], args...) +
    interp_atmos_time_series(ΣJ[2], args...)

@inline interp_atmos_time_series(ΣJ::Tuple{<:Any, <:Any, <:Any}, args...) =
    interp_atmos_time_series(ΣJ[1], args...) +
    interp_atmos_time_series(ΣJ[2], args...) +
    interp_atmos_time_series(ΣJ[3], args...)

@inline interp_atmos_time_series(ΣJ::Tuple{<:Any, <:Any, <:Any, <:Any}, args...) =
    interp_atmos_time_series(ΣJ[1], args...) +
    interp_atmos_time_series(ΣJ[2], args...) +
    interp_atmos_time_series(ΣJ[3], args...) +
    interp_atmos_time_series(ΣJ[4], args...)

@inline interp_atmos_time_series(ΣJ::Tuple{<:Any, <:Any, <:Any, <:Any, <:Any}, args...) =
    interp_atmos_time_series(ΣJ[1], args...) +
    interp_atmos_time_series(ΣJ[2], args...) +
    interp_atmos_time_series(ΣJ[3], args...) +
    interp_atmos_time_series(ΣJ[4], args...) +
    interp_atmos_time_series(ΣJ[5], args...)

@inline interp_atmos_time_series(ΣJ::Tuple, args...) =
    interp_atmos_time_series(ΣJ[1], args...) +
    interp_atmos_time_series(ΣJ[2:end], args...)
